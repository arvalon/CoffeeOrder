apply plugin: 'com.android.application'
apply plugin: 'realm-android'

android {

    compileSdkVersion 30
    defaultConfig {
        applicationId "ru.arvalon.coffeeorder"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 2
        versionName "1.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {jvmArgs '-noverify'}
        }
    }

    repositories {
        //maven { url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo' }
        maven { url 'https://github.com/WickeDev/stetho-realm/raw/master/maven-repo' }
    }
}

configurations.all { config ->
    if (config.name.contains('UnitTest')) {
        config.resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
                details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    //implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // General testing
    testImplementation 'junit:junit:4.13.1'

    // https://github.com/robolectric/robolectric/issues/3839
    testImplementation 'androidx.test:runner:1.3.0'
    testImplementation 'androidx.test:rules:1.3.0'

    // Mockito
    testImplementation 'org.mockito:mockito-core:2.17.0'
    androidTestImplementation 'org.mockito:mockito-android:2.17.0'

    // Robolectric
    testImplementation 'org.robolectric:robolectric:4.2.1'

    // Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Отслеживание утечек памяти
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    // Stetho chrome://inspect
    debugImplementation 'com.facebook.stetho:stetho:1.5.1'
    debugImplementation 'com.uphyca:stetho_realm:2.2.2'
}
